apply plugin: 'java'
apply plugin: 'maven'

archivesBaseName = 'laggedHero-annotations'

sourceSets {
    main.java.srcDir 'src'
}

jar {
    from sourceSets.main.output
}

task writePom << {
    pom {
        project {
            name 'LaggedHero Annotations'
            description "Some random annotations."
            url 'https://github.com/laggedHero/annotations'
            inceptionYear '2015'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'LaggedHero'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}

// configuration for the javadoc to include all source sets.
javadoc {
    source    sourceSets.main.allJava
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier         'javadoc'
    from               javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
